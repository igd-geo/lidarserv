WHITESPACE   = _{ " " | "\t" | "\n" }
digit        =  { '0'..'9' }
integer      = @{ digit+ }
number       = @{ digit+ ~ ("." ~ digit+)? }
coordinate   =  { "[" ~ number ~ "," ~ number ~ "," ~ number ~ "]" }
coordinate2  =  { "[" ~ number ~ "," ~ number ~ "]" }
empty        =  { "empty" }
full         =  { "full" }
lod          =  { "lod" ~ "(" ~ integer ~ ")" }
aabb         =  { "aabb" ~ "(" ~ coordinate ~ "," ~ coordinate ~ ")" }
view_frustum_arg = _{
    ("camera_pos" ~ ":" ~ #cp=coordinate) 
    | ("camera_dir" ~ ":" ~ #cd=coordinate) 
    | ("camera_up" ~ ":" ~ #cu=coordinate) 
    | ("fov_y" ~ ":" ~ #fov=number) 
    | ("z_near" ~ ":" ~ #zn=number) 
    | ("z_far" ~ ":" ~ #zf=number) 
    | ("window_size" ~ ":" ~ #ws=coordinate2) 
    | ("max_distance" ~ ":" ~ #md=number)
}
view_frustum =  {"view_frustum" ~ "(" ~ view_frustum_arg ~ ("," ~ view_frustum_arg)* ~ ")"}
bracket      =  { "(" ~ query ~ ")" }
atom         = _{ empty | full | lod | aabb | view_frustum | bracket }
not          =  { "!" ~ atom }
not_query    = _{ not | atom }
and          =  { not_query ~ ("and" ~ not_query)+ }
and_query    = _{ and | not_query }
or           =  { and_query ~ ("or" ~ and_query)+ }
or_query     = _{ or | and_query }
query        = _{ or_query }
full_query   = _{ query ~ EOI }
